#+ defaults:
#+   - graph: http://ontosoft.isi.edu:3030/modelCatalog-1.2.0/data/mint@isi.edu
#+   - text: pongo
#+   - type: https://w3id.org/okn/o/sdm#ModelConfiguration
#+ summary: Given a rdf type, returns all the resources related to the type

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sd: <https://w3id.org/okn/o/sd#>
PREFIX sdm: <https://w3id.org/okn/o/sdm#>

CONSTRUCT {
    ?item ?predicate ?prop .
    ?prop a ?type
}
WHERE {
    GRAPH ?_g_iri {
        {
            SELECT DISTINCT ?item where {
                ?item a ?_type_iri .
                ?item ?prop ?desc .
                VALUES ?prop { sd:description rdfs:label sd:keywords sdm:hasModelCategory }    
                filter regex(str(?desc),?_text,"i")
            }
        }
        ?item ?predicate ?prop
        OPTIONAL {
         ?prop a ?type
        }
    }
}