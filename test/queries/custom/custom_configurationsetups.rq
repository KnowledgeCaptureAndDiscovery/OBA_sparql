PREFIX sd: <https://w3id.org/okn/o/sd#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
CONSTRUCT {
?_resource_iri ?predicate ?prop .
?prop ?p ?o .
?input_resource ?input_resource_p ?input_resource_o .
?output_resource ?output_resource_p ?output_resource_o .
?parameter ?parameter_p ?parameter_o
}
WHERE {
    GRAPH ?_g_iri {
        {
            ?_resource_iri ?predicate ?prop .
            OPTIONAL {
                ?prop ?p ?o
            }
        }
        UNION {
            ?_resource_iri  sd:hasInput ?input .
            ?input sd:hasFixedResource ?input_resource  .
            ?input_resource ?input_resource_p ?input_resource_o
        }
        UNION {
            ?_resource_iri  sd:hasOutput ?output .
            ?output sd:hasFixedResource ?output_resource  .
            ?output_resource ?output_resource_p ?output_resource_o
        }
        UNION {
            ?_resource_iri  sd:hasParameter ?parameter .
            ?parameter ?parameter_p ?parameter_o
        }
    }
}
